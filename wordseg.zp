/******************************************************************************\
FILE:           wordseg.zp
AUTHOR:         Theo Veenker (UiL-OTS) <T.J.G.Veenker@uu.nl>
ADAPTED BY:     Martijn van der Klis (UiL-OTS) <M.H.vanderKlis@uu.nl>
                Maarten Duijndam

EXPERIMENT:     Preferential Listening, Segmentation Experiment (using eye-tracking)

DESCRIPTION:

Purpose of this experiment is to detect whether an infant participant
has a preference for one particular type of sound (word, intonation, 
accent, language etc) over another. The infant sits on the caregiver's 
lap facing a computer monitor on which visual stimuli will be presented.
Auditory stimuli will be presented through a speaker. The infants 
eye-movements will be followed by an eye-tracker. The procedure 
consists of a short familiarization phase, and a test phase. The trial 
structure is the same for both phases: first an audio-visual attention 
getter is presented. Then a visual stimulus is presented together with 
an auditory stimulus. The auditory stimulus consists of a sequence of 
sounds (words) of a particular type. For each in the test phase trial 
the accumulated time the child looks at the visual stimulus is measured.
If the infant looks away too long during the test phase, the trial is 
aborted. Output: looking time.


Global structure of experiment:

  - welcome

  - eye-tracker calibration

  - familiarization phase (1 trial)
      trial

  - test phase (N trials)
      trial

  - thankyou


HISTORY:
2012-11-10 TV   Created.
2013-06-28 MvdK Added groups for familiarization stimuli.
2013-07-02 MvdK Added randomization for both familiarization and test items.
2014-02-28 MvdK Converted script to ZEP version 1.4.
2014-03-28 MvdK Converted script to ZEP version 1.6 (only changed "requires" line)
2014-04-03 ATM  Added output for familiarization phase.
2017-02-01 MD   Converted script to ZEP version 1.14 (only changed "requires" line)
2017-02-01 MD   Moved the familiarization and test phase to the test directory
                in order to be compatible with how an experiment should look
                today.
\******************************************************************************/

requires 1.16;

import std_windows1;
import std_texts_en;
import std_entry_page2;
import std_exit_page2;
import std_pause_page;
import std_blank_page;
import std_sound_output_device;
import std_sound_playback;

import test::eyetracker_eyelink_infant;
import test::fam_task;
import test::test_task;

Experiment experiment
{
    on_event:entry()
    {
        show_test_windows();
    }


    on_event:message()
    {
        // On Ctrl+F4 jump unconditionally to thankyou phase.
        if (message_sender == control && message_arg == SYS_STOP)
        {
            control.mark_experiment_aborted();
            thankyou.enter();
        }
    }


    Part welcome
    {
        on_event:entry()
        {
            entry_page.add_image(stimuli_dir() + "images/bal.png");
            entry_page.action(this);
        }
    }

    Part familiarization
    {
        on_event:entry()
        {
            test::fam_action(this);
        }
    }
    
    Part test
    {
        on_event:entry()
        {
            test::test_action(this);
        }
    }

    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms, 500ms);
        }
    }


    Part thankyou
    {
        int     sesid;


        on_event:entry()
        {
            sesid = expdb.experiment.session.id;

            control.end_experiment();   // Check-in session.

            exit_page.add_image(stimuli_dir() + "images/giraf.png");
            exit_page.add_image(stimuli_dir() + "images/kikker.png");
            exit_page.add_image(stimuli_dir() + "images/kip.png");
            exit_page.add_image(stimuli_dir() + "images/koe.png");
            exit_page.add_image(stimuli_dir() + "images/konijn.png");
            exit_page.add_image(stimuli_dir() + "images/pinguin.png");
            exit_page.add_image(stimuli_dir() + "images/vlinder.png");
            exit_page.add_image(stimuli_dir() + "images/zebra.png");
            exit_page.action(this, 60s);
            sound_playback.playback.set_scaling(1.0);
            sound_playback.play(stimuli_dir() + "sounds/handjes.wav", 
                now() + 1000ms);
        }
    }
}
